import swhModel : 'platform:/resource/fr.inria.diverse.swhModel/model/swhModel.ecore'
package swhModel
context Graph
def : query():Set(Origin) = origins->select(
	 getLastSnapshot().branches->exists(
	 				(name= 'refs/heads/master' or name= 'refs/heads/main')
					and
					/*The main branch contains at least 1000 revisions */
					getRevision()->closure(parent)-> size() >1000
					and
					/*The first revision have been created since 2015 */
					getRevision().getRootRevision().commiterTimestamp>1420066800
					and
					/*The main branch contains a file 'AndroidManifest.xml'*/
					getRevision().tree.entries->closure(entry:DirectoryEntry |
						if entry.child.oclIsKindOf(Directory) then
							entry.child.oclAsType(Directory).entries.oclAsSet()
						else 
							entry.oclAsSet()
						endif	
					)->exists(e:DirectoryEntry | e.name='AndroidManifest.xml')	
	)
)

context Revision
/**
 * Get the first Revision transitively, in case of merge commit only one of the commit are handle 
  */
 def : getRootRevision() : Revision =
		if parent = null then
			self
		else 
			parent.getRootRevision()
		endif
endpackage
